RS BIT P3.0
EN BIT P3.1

TMP_COUNT EQU 30H
COUNT EQU 31H
TMP_TH1 EQU 32H
TMP_TL1 EQU 33H

COUNT_ET0 EQU 34H

SUBB1_H EQU 35H
SUBB1_L EQU 36H
SUBB2_H EQU 37H
SUBB2_L EQU 38H
SUBB_RS_H EQU 39H
SUBB_RS_L EQU 3AH

DIV_RS EQU 3BH
DIV_RM EQU 3CH

ORG 00H
LJMP MAIN

ORG 0BH
LJMP ISR_ET0

ORG 1BH
LJMP ISR_ET1


ORG 30H
MAIN:
		; LCD INIT
	MOV A, #38H			; LCD1602
	CALL LCD_WR_CMD
	MOV A, #0CH			; TURN ON LCD
	CALL LCD_WR_CMD
	MOV A, #1H			; CLEAR LCD
	CALL LCD_WR_CMD

	; Timer
	MOV TMOD, #51H		; Timer 0, Mode 1;  Timer 1, mode 1 C/T=1
	MOV TL1, #0B2H		; Count 334 pulse
	MOV TH1, #0FEH
	SETB ET1
	MOV TMP_COUNT, #0
	MOV COUNT, #0
	
	
	MOV TL0, #0F0H		; 10 ms
	MOV TH0, #0D8H
	SETB ET0
	MOV COUNT_ET0, #99	; 10 ms x 100 = 1s
	
	SETB EA				; Enable interrupt

	SETB TR0
	SETB TR1


MAIN_LOOP:
	 
	CALL SHOW
	MOV R7, #200
	CALL DELAY

	JMP MAIN_LOOP

SHOW:
	MOV A, #1H		; CLEAR LCD
	CALL LCD_WR_CMD
	
	MOV A, COUNT
	MOV B, #60
	MUL AB			; RPM value. A - 8 LOW bit; B - 8 HIGH BIT

	MOV R5, A
	MOV R4, B
	; thousands
	MOV SUBB1_H, R4
	MOV SUBB1_L, R5
	MOV SUBB2_H, #3H	 ; 1000
	MOV SUBB2_L, #0E8H
	CALL DIV16			 ; Get thousands of RPM
	MOV A, DIV_RS
	ADD A, #48
	CALL LCD_WR_DATA

	; hundred
   	MOV SUBB1_H, R4
	MOV SUBB1_L, R5
	MOV SUBB2_H, #0H	 ; 100
	MOV SUBB2_L, #100
	CALL DIV16
	MOV A, DIV_RS
	MOV B, #10
	DIV AB
	MOV A, B
	ADD A, #48
	CALL LCD_WR_DATA

	; Dozens
	MOV A, DIV_RM
	MOV B, #10
	DIV AB
	ADD A, #48
	CALL LCD_WR_DATA

	; per
	MOV A, B
	ADD A, #48
	CALL LCD_WR_DATA
	
	RET

LCD_WR_CMD:
	MOV P2, A
	CLR RS
	CLR EN
	SETB EN
	MOV R7, #10 ; DELAY 
	CALL DELAY
	RET

LCD_WR_DATA:
	MOV P2, A
	SETB RS
	CLR EN
	SETB EN
	MOV R7, #1 ; DELAY 
	CALL DELAY
	RET

DELAY:
	; R7 - time	x 0.5 ms
DELAY_LOOP1:
	MOV R6, #250
DELAY_LOOP2:
	DJNZ R6, DELAY_LOOP2
	DJNZ R7, DELAY_LOOP1
	RET

;-----------------
SUBB16:
	; S1:  SUBB1_H SUBB1_L
	; S2:  SUBB2_H SUBB2_L
	; RS:  SUBB_RS_H SUBB_RS_L
	CLR C
	MOV A, SUBB1_L
	MOV B, SUBB2_L																											  
	SUBB A, B
	MOV SUBB_RS_L, A	; RS - 8LB
	
	MOV A, SUBB1_H
	MOV B, SUBB2_H
	SUBB A, B
	MOV SUBB_RS_H, A	; RS - 8HB
	RET

DIV16:
	MOV SUBB_RS_H, #0	;	RS - 8HB
	MOV SUBB_RS_L, #0	;	RS - 8LB
	MOV DIV_RS, #0		; 	
	MOV DIV_RM, #0		; 	
DIV16_LOOP:
	CALL SUBB16
	JC END_DIV16
	INC DIV_RS			; 
	
	MOV R1, SUBB_RS_H
	MOV R2, SUBB_RS_L
	
	MOV SUBB1_H, R1
	MOV SUBB1_L, R2	

	CJNE R1, #0, DIV16_LOOP
	MOV A, R2
	MOV B, SUBB2_L
	SUBB A, B
	JNC DIV16_LOOP
	MOV	DIV_RM, R2	
END_DIV16:
	RET

;-----------------
ISR_ET0:
	MOV TL0, #0F0H		; count 1s
	MOV TH0, #0D8H
	CLR TF0
	DJNZ COUNT_ET0, END_ISR_ET0
	MOV COUNT_ET0, #99
	MOV COUNT, TMP_COUNT
	MOV TMP_COUNT, #0
	MOV TMP_TH1, TH1
	MOV TMP_TL1, TL1
END_ISR_ET0:
	RETI

;-----------------
ISR_ET1:
	MOV TL1, #0B2H		; Count 334 pulse
	MOV TH1, #0FEH
	INC TMP_COUNT
	CLR TF1
END_ISR_ET1:
	RETI
END